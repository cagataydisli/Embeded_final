;===============================================================
; FiDEX Konfigurasyonu (Pico2)
;===============================================================
#ifdef PROC:: XPBLAZE6
    #set proc::xPblaze6::scrpdSize,       64
    #set proc::xPblaze6::clkFreq,         100000000
    #set IODev:: defaultInstMem::en,               TRUE
    #set IODev:: defaultInstMem::type,             mem
    #set IODev:: defaultInstMem::size,             1024
    #set instmem::pageSize,                        1024
    #set instmem::pageCount,                       1
    #set instmem::sharedMemLocation,               loMem
    #set IODev:: defaultInstMem::value,            instmem
    #set IODev:: defaultInstMem::vhdlen,           TRUE
    #set IODev:: defaultInstMem::vhdlEntityName,   "pico2_rom"
    #set IODev:: defaultInstMem::vhdlTmplFile,     "ROM_form.vhd"
    #set IODev:: defaultInstMem::vhdlTargetFile,   "pico2_rom.vhd"
#endif

;===============================================================
; Port Tanımları (HEX formatında - top.v ile uyumlu)
;===============================================================
; Mem2 (Key)
#EQU port_mem2_addr,   0x20
#EQU port_mem2_data,   0x21

; FIFO
#EQU port_fifo_read,   0x22
#EQU port_fifo_status, 0x23

; XTEA
#EQU port_xtea_key,    0x30
#EQU port_xtea_data,   0x31
#EQU port_xtea_ctrl,   0x33
#EQU port_xtea_ready,  0x34
#EQU port_xtea_res,    0x35

; Mem3 (Result)
#EQU port_mem3_addr,   0x40
#EQU port_mem3_data,   0x41

;===============================================================
; Register Tanımları
;===============================================================
#EQU s_cnt,        s0      ; Genel Sayaç / Byte İşaretçisi
#EQU s_data,       s1      ; Veri ve Durum Registeri
#EQU s_res_ptr,    s2      ; Sonuç Adres İşaretçisi

;===============================================================
; MAIN PROGRAM (Adres 0x000)
;===============================================================
#ORG ADDR, 0
START:
    LOAD s_res_ptr, 0x00     ; Sonuç işaretçisini sıfırla

MAIN_LOOP:
    CALL READ_KEY
    CALL READ_DATA_FIFO
    CALL RUN_XTEA
    CALL READ_WRITE_RESULT
    
    ; İşlem bitti, durdurabiliriz veya başa dönebiliriz
    JUMP STOP

;===============================================================
; 1. READ KEY (Mem2'den 16 Byte Oku ve XTEA'ya yükle)
;===============================================================
READ_KEY:
    LOAD s_cnt, 0x00         ; Sayaç sıfırla
READ_KEY_LOOP:
    WRPRT s_cnt, port_mem2_addr    ; Adresi Port 20'ye yaz
    RDPRT  s_data, port_mem2_data   ; Veriyi Port 21'den oku
    WRPRT s_data, port_xtea_key    ; XTEA Key Portuna (30) yaz
    
    ADD s_cnt, 0x01
    COMP s_cnt, 0x10         ; 16 Byte (0x10) oldu mu?
    JUMP NZ, READ_KEY_LOOP
    RET

;===============================================================
; 2. READ DATA (FIFO'dan 8 Byte Oku ve XTEA'ya yükle)
;===============================================================
READ_DATA_FIFO:
    LOAD s_cnt, 0x00
READ_FIFO_LOOP:
    ; FIFO boş mu kontrol et, dolana kadar bekle
WAIT_FIFO_NOT_EMPTY:
    RDPRT s_data, port_fifo_status  ; FIFO durumunu oku (Port 23)
    TEST s_data, 0x01                 ; empty bit (Bit 0) 1 mi?
    JUMP NZ, WAIT_FIFO_NOT_EMPTY    ; Boşsa bekle
    
    ; FIFO'da veri var, oku
    RDPRT s_data, port_fifo_read    ; FIFO'dan oku (Port 22)
    WRPRT s_data, port_xtea_data   ; XTEA Data Portuna (31) yaz
    
    ADD s_cnt, 0x01
    COMP s_cnt, 0x08         ; 8 Byte oldu mu?
    JUMP NZ, READ_FIFO_LOOP
    RET

;===============================================================
; 3. RUN XTEA (Şifrelemeyi Başlat)
;===============================================================
RUN_XTEA:
    ; Mode Setup:
    ; 03h = Decrypt + Start (Bit 1=1, Bit 0=1)
    ; 01h = Encrypt + Start (Bit 1=0, Bit 0=1)
    LOAD s_data, 0x01        ; Decrypt Modu
    WRPRT s_data, port_xtea_ctrl

WAIT_READY:
    RDPRT s_data, port_xtea_ready
    TEST s_data, 0x01        ; Ready biti (Bit 0) 1 mi?
    JUMP Z, WAIT_READY     ; 0 ise bekle
    RET

;===============================================================
; 4. READ RESULT (XTEA'dan oku, Mem3'e yaz)
;===============================================================
READ_WRITE_RESULT:
    LOAD s_cnt, 0x00
    
RESULT_LOOP:
    RDPRT s_data, port_xtea_res      ; Sonucu oku (Port 35)
    
    WRPRT s_res_ptr, port_mem3_addr ; Mem3 Adresini ayarla
    WRPRT s_data, port_mem3_data    ; Mem3 Verisini yaz
    
    ADD s_res_ptr, 0x01                ; Sonuç işaretçisini arttır
    ADD s_cnt, 0x01                    ; Döngü sayacını arttır
    
    COMP s_cnt, 0x08                   ; 8 Byte bitti mi?
    JUMP NZ, RESULT_LOOP
    RET

STOP:
    JUMP STOP ; Sonsuz döngü
